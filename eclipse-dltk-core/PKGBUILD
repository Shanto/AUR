# Maintainer: Shanto <shanto@hotmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgname=eclipse-dltk-core
pkgver=3.0.1
pkgrel=1
_pkgdate=201108261011
pkgdesc="Support for dynamic languages in Eclipse (Stable Stream)"
arch=('i686' 'x86_64')
url="http://www.eclipse.org/dltk/"
license=('EPL')
depends=("eclipse-emf")
optdepends=("eclipse-dltk-core-index" "eclipse-dltk-javascript" "eclipse-dltk-mylyn" "eclipse-dltk-python" "eclipse-dltk-rse" "eclipse-dltk-ruby" "eclipse-dltk-tcl")
_mirror="http://www.eclipse.org/downloads/download.php?r=1&file="
source=("$_mirror/technology/dltk/downloads/drops/R${pkgver:0:3}/S-$pkgver-$_pkgdate/${pkgname#eclipse-}-S-$pkgver-$_pkgdate.zip")
md5sums=('5b8cb71e7be7ef951e836de8c7f80779')

package() {
	cd $srcdir
	install -dm755 $pkgdir/usr/share/eclipse/dropins/${pkgname#eclipse-}/
	find eclipse -type f -exec install -Dm644 {} \
		$pkgdir/usr/share/eclipse/dropins/${pkgname#eclipse-}/{} \;
}

