# Maintainer: Shanto <shanto@hotmail.com>
# Contributor: Shanto <shanto@hotmail.com>

pkgname=eclipse-shelled
pkgver=2.0.1
pkgrel=1
pkgdesc="Superb shell script editor for Eclipse"
arch=('any')
url="http://shelled.sourceforge.net/"
license=('EPL')
depends=("eclipse" "eclipse-dltk-core" "eclipse-linuxtools")
optdepends=('openssh')
_mirror="http://freefr.dl.sourceforge.net"
source=("$_mirror/project/shelled/shelled/ShellEd%20${pkgver}/net.sourceforge.shelled-site-${pkgver}.zip")
md5sums=('cc30e193393f928f8cd57f587fb2f58a')

package() {
	_dest=$pkgdir/usr/share/eclipse/dropins/${pkgname#eclipse-}/eclipse
	cd $srcdir
	install -dm755 $_dest/
	find features plugins -type f -name '*.jar' | while read jar ; do
		install -dm755 ${_dest}/${jar%*.jar}
		cd ${_dest}/${jar/.jar}
		jar xf ${srcdir}/${jar}
	done
	find $_dest/plugins -name MANIFEST.MF \
		-exec sed -i 's|eclipse.linuxtools.man,|eclipse.linuxtools.man.core,|g' {} \;
}
