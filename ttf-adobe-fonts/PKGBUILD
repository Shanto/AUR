# Maintainer: Shanto (at Hotmail)
# Contributor: Roger Braun <davinelulinvega@gmail.com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: kevku <kevku@gmx.com>

pkgname=ttf-adobe-fonts
pkgver=10.1.2
pkgrel=1
arch=('any')
pkgdesc="Fonts from Adobe Acrobat Reader X including Myriad Pro, Myriad CAD, Minion Pro, Courier Std and Adobe Pi Std"
url="http://www.adobe.com/products/reader.html"
source=(
	"http://ardownload.adobe.com/pub/adobe/reader/win/10.x/10.1.2/en_US/AdbeRdr1012_en_US.exe"
	"LICENSE"
)
md5sums=(
	'a8fd47ec1de9369f835bd707bd5f4ddb'
	'd4d2d30b83bb8e9578ebc22676e328b3'
)
makedepends=('p7zip')
conflicts=('acroread-fonts-systemwide')
install=${pkgname}.install
license=('custom')
noextract=($(basename ${source[0]}))

package() {
	cd "$srcdir"
	7z -y e $(basename ${source[0]})
	mkdir -p $pkgdir/usr/share/{fonts/OTF,licenses/$pkgname}
	7z -y -o"$pkgdir/usr/share/fonts/OTF" -ir\!*.otf e Data1.cab
	install -m 0644 LICENSE $pkgdir/usr/share/licenses/$pkgname/
}
